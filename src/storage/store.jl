

"""
    Store(database)

Unifies the `Compressor` module and the database. The `Store` handles the
deduplication process by storing the bases generated by the `Compressor` into
the `database`.
"""
mutable struct Store
    compressor::Compressor
    database::Dict{Vector{UInt8}, Vector{UInt8}}
    # stats
    l::ReentrantLock
    num_unknown_bases::Int
    num_requested_bases::Int

    Store(c, d, nub, nrb) = new(c, d, ReentrantLock(), nub, nrb)
end


"""
    update!(store, hashes}, bases)

Updates `store.database` by mapping `hashes` to `bases`.
"""
function update!(s::Store, hashes::Vector{Vector{UInt8}}, bases::Vector{Vector{UInt8}})
    for (h, b) ∈ zip(hashes, bases)
        s.database[h] = b
    end
end


"""
    compress!(store, data})

Stores the bases generated from `data` into the `database` and returns a
compressed version of `data` as a `GDFile`.
"""
function compress!(s::Store, data::Vector{T})::GDFile where T <: Unsigned
    gdfile, bases = compress(s.compressor, data)
    update!(s, gdfile.hashes, bases)
    return gdfile
end


"""
    extract(store::Store, gdfile::GDFile)

Decompresses the `gdfile` into its original representation. This methods assumes
that a valide `GDFile` is given as input (the `validate()` method must return
∅).
"""
function extract(s::Store, gdfile::GDFile)::Vector
    bases = get(s, gdfile.hashes)
    return extract(s.compressor, gdfile, bases)
end


"""
    validate(store::Store, gdfile::GDFile)

Checks wether `gdfile` can be extracted by `store` or not by returning the list 
of unknown hashes used by `gdfile`. The `GDFile` is said valid if `validate()`
returns ∅.
"""
function validate(s::Store, gdfile::GDFile)::Vector{Vector{UInt8}}
    return setdiff(gdfile.hashes, keys(s.database))
end


"""
    get(store, hashes)

returns the values mapped to `hashes` in `store`.
"""
function get(s::Store, hashes::Vector{Vector{UInt8}})::Vector{Vector{UInt8}}
    return [s.database[hash] for hash in hashes]
end
