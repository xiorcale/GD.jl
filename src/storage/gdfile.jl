"""
    GDFile(hashes, deviations, padsize)

Data structure holding the compressed representation of data generated by a
compressor. Suitable for storing or exchanging over network.
"""
mutable struct GDFile
    hashes::Vector{Vector{UInt8}}
    deviations::Vector{Vector{UInt8}}
    padsize::Int
end


"""
    diff(x, y)

Compute the difference between `x` and `y`, and return either the `x` if they
are different or `0x00` if they are the same.
"""
diff(x::Vector{UInt8}, y::Vector{UInt8}) = (x == y ? [0x00] : x)


"""
    patch!(gdfile1, gdfile2)

Patches `gdfile1` by replacing the hashes/deviations which are the same as
`gdfile2` by `0x00`.
"""
function patch!(gdfile1::GDFile, gdfile2::GDFile)
    gdfile1.hashes = diff.(gdfile1.hashes, gdfile2.hashes)
    gdfile1.deviations = diff.(gdfile1.deviations, gdfile2.deviations)
end


"""
    unpatch!(gdfile1, gdfile2)

Reverse `patch!()` by replaxing `0x00` in `gdfile1` by the value contianed in
`gdfile2`.
"""
function unpatch!(gdfile1::GDFile, gdfile2::GDFile)
    for i in 1:length(gdfile1.hashes)
        if gdfile1.hashes[i] == [0x00]
            gdfile1.hashes[i] = gdfile2.hashes[i]
        end
        if gdfile1.deviations[i] == [0x00]
            gdfile1.deviations[i] = gdfile2.deviations[i]
        end
    end
end